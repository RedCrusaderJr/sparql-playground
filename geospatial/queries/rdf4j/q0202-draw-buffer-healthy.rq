#title:draw-buffer-healthy
#comment: conducting equipment (ACL segment) NOT AFFECTED BY HAZARD, with buffer wkt polygon and wkt linestring

#tags:buffer,wkt,coordinates
#color:ex-blue
#img:

SELECT DISTINCT ?conductingEquipmentId ?g_wktSegmentLine ?g_wktBufferPolygon WHERE
{
    {
        #conducting equipment (ACL segment) with buffer wkt polygon and wkt linestring     
        SELECT DISTINCT ?conductingEquipmentId ?g_wktSegmentLine ?g_wktBufferPolygon WHERE 
        {
            {
                #conducting equipment with coordinates
                SELECT DISTINCT ?conductingEquipmentId ?x1 ?y1 ?x2 ?y2 WHERE 
                {
                    #terminal with a connectivityNode and a conducting equipment
                    ?terminalId a cim:Terminal;
                        cim:Terminal.ConnectivityNode ?connectivityNode;
                        cim:Terminal.ConductingEquipment ?conductingEquipmentId.							   
                        
                    #cartesian product of position points
                    ?postionPointId1 rdf:type cim:PositionPoint.
                    ?postionPointId2 rdf:type cim:PositionPoint.
                    #reducing cartesian product
                    FILTER(str(?postionPointId1) < str(?postionPointId2))

                    #conducting equipment with a location
                    ?conductingEquipmentId cim:PowerSystemResource.Location ?locationId.
                    
                    #position point 1 of location with coordinates
                    ?postionPointId1 cim:PositionPoint.Location ?locationId;
                        cim:PositionPoint.xPosition ?x1;
                        cim:PositionPoint.yPosition ?y1.
                    
                    #position point 2 of location with coordinates
                    ?postionPointId2 cim:PositionPoint.Location ?locationId;
                        cim:PositionPoint.xPosition ?x2;
                        cim:PositionPoint.yPosition ?y2.

                    #reducing coordinates permutations
                    FILTER(?x1 != ?x2 || ?y1 != ?y2)
                }
            }

            #hazard with hazard zone polygone
            ?hazardType rdfs:subClassOf+ custom:Hazard. 
            ?hazardId a ?hazardType;
                custom:Hazard.DangerZone ?g_wktHazardZone.

            #construct LINESTRING WKT
            BIND(concat("LINESTRING (", str(?x1), " ", str(?y1), ", ", str(?x2), " ", str(?y2), ")") as ?segmentLineStr).
            BIND(strdt(?segmentLineStr, geo:wktLiteral) as ?g_wktSegmentLine).

            #construct buffer POLYGON WKT
            BIND(<http://example.org/custom-function/buffer>(?segmentLineStr, 50) as ?bufferPolygonStr).
            BIND(strdt(?bufferPolygonStr, geo:wktLiteral) as ?g_wktBufferPolygon).
        }
    }
    
    MINUS 
    {
        #conducting equipment (ACL segment) AFFECTED BY HAZARD, with buffer wkt polygon and wkt linestring     
        SELECT DISTINCT ?conductingEquipmentId ?g_wktSegmentLine ?g_wktBufferPolygon WHERE 
        {
            {
                #conducting equipment with coordinates
                SELECT DISTINCT ?conductingEquipmentId ?x1 ?y1 ?x2 ?y2 WHERE 
                {
                    #terminal with a connectivityNode and a conducting equipment
                    ?terminalId a cim:Terminal;
                        cim:Terminal.ConnectivityNode ?connectivityNode;
                        cim:Terminal.ConductingEquipment ?conductingEquipmentId.							   
                        
                    #cartesian product of position points
                    ?postionPointId1 rdf:type cim:PositionPoint.
                    ?postionPointId2 rdf:type cim:PositionPoint.
                    #reducing cartesian product
                    FILTER(str(?postionPointId1) < str(?postionPointId2))

                    #conducting equipment with a location
                    ?conductingEquipmentId cim:PowerSystemResource.Location ?locationId.
                    
                    #position point 1 of location with coordinates
                    ?postionPointId1 cim:PositionPoint.Location ?locationId;
                        cim:PositionPoint.xPosition ?x1;
                        cim:PositionPoint.yPosition ?y1.
                    
                    #position point 2 of location with coordinates
                    ?postionPointId2 cim:PositionPoint.Location ?locationId;
                        cim:PositionPoint.xPosition ?x2;
                        cim:PositionPoint.yPosition ?y2.

                    #reducing coordinates permutations
                    FILTER(?x1 != ?x2 || ?y1 != ?y2)
                }
            }

            #hazard with hazard zone polygone
            ?hazardType rdfs:subClassOf+ custom:Hazard. 
            ?hazardId a ?hazardType;
                custom:Hazard.DangerZone ?g_wktHazardZone.

            #construct LINESTRING WKT
            BIND(concat("LINESTRING (", str(?x1), " ", str(?y1), ", ", str(?x2), " ", str(?y2), ")") as ?segmentLineStr).
            BIND(strdt(?segmentLineStr, geo:wktLiteral) as ?g_wktSegmentLine).

            #construct buffer POLYGON WKT
            BIND(<http://example.org/custom-function/buffer>(?segmentLineStr, 50) as ?bufferPolygonStr).
            BIND(strdt(?bufferPolygonStr, geo:wktLiteral) as ?g_wktBufferPolygon).

            #filter by GeoSPARQL intersection of buffer and hazard polygon
            BIND(geof:intersection(?g_wktBufferPolygon, ?g_wktHazardZone) as ?intersection).
            FILTER(str(?intersection) != "POINT EMPTY")
        }
    }
}