#title:draw-affected
#comment:

#tags:intersection,connected-points,wkt,coordinates
#color:ex-green
#img:

SELECT DISTINCT ?g_wktPolygon ?g_wktLine
WHERE {
  #terminals
  ?tmId rdf:type cim:Terminal.
  ?tmId cim:IdentifiedObject.name ?tmName.
  
  #connectivity nodes of terminal
  ?tmId cim:Terminal.ConnectivityNode ?cNodeId.
  ?cNodeId cim:IdentifiedObject.name ?cNodeName.
  
  #conducting equipment of terminal
  ?tmId cim:Terminal.ConductingEquipment ?condEqId.
  ?condEqId cim:IdentifiedObject.name ?condEqName.
  
  #locations
  ?condEqId cim:PowerSystemResource.Location ?locationId.
  ?postionPointId1 rdf:type cim:PositionPoint.
  ?postionPointId2 rdf:type cim:PositionPoint.
  
  ?postionPointId1 cim:PositionPoint.Location ?locationId.
  ?postionPointId2 cim:PositionPoint.Location ?locationId.
  
  #coordinates
  ?postionPointId1 cim:PositionPoint.xPosition ?x1.
  ?postionPointId1 cim:PositionPoint.yPosition ?y1.
  
  ?postionPointId2 cim:PositionPoint.xPosition ?x2.
  ?postionPointId2 cim:PositionPoint.yPosition ?y2.
  
  #hazard zones
  ?hazardType rdfs:subClassOf+ custom:Hazard. 
  ?hazardId rdf:type ?hazardType.
  ?hazardId custom:Hazard.DangerZone ?g_wktHazardZone.

  #construct WKTs (draw geometry)
  BIND(concat("POINT (", str(?x1), " ", str(?y1), ")") as ?pointStr)
  BIND(strdt(?pointStr, geo:wktLiteral) as ?g_wktPoint)
  
  BIND(concat("LINESTRING (", str(?x1), " ", str(?y1), ", ", str(?x2), " ", str(?y2), ")") as ?lineStr).
  BIND(strdt(?lineStr, geo:wktLiteral) as ?g_wktLine).
  
  BIND(<http://example.org/custom-function/buffer>(?lineStr, 50) as ?polygonStr).
  BIND(strdt(?polygonStr, geo:wktLiteral) as ?g_wktPolygon).
  
  BIND(geof:intersection(?g_wktPolygon, ?g_wktHazardZone) as ?intersection).
  FILTER(str(?intersection) != "POINT EMPTY")

  FILTER(?x1 != ?x2 || ?y1 != ?y2)
  FILTER(str(?postionPointId1) < str(?postionPointId2))
}