#title:test1
#comment: draw feeder

#tags:
#color:
#img:

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX geo: <http://www.opengis.net/ont/geosparql#> 
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>  
PREFIX cim: <http://iec.ch/TC57/CIM-generic#> 
PREFIX xdmp: <http://marklogic.com/xdmp#> 
PREFIX custom: <http://www.custom.org/> 

SELECT DISTINCT ?g_wktLine ?g_wktPoint
WHERE {
  #terminals
  ?tmId rdf:type cim:Terminal.
  ?tmId cim:IdentifiedObject.name ?tmName.
   
  #connectivity nodes of terminal
  ?tmId cim:Terminal.ConnectivityNode ?cNodeId.
  ?cNodeId cim:IdentifiedObject.name ?cNodeName.
  
  #conducting equipment of terminal
  ?tmId cim:Terminal.ConductingEquipment ?condEqId.
  ?condEqId cim:IdentifiedObject.name ?condEqName.
  
  #locations
  ?condEqId cim:PowerSystemResource.Location ?locationId.
  ?postionPointId1 rdf:type cim:PositionPoint.
  ?postionPointId2 rdf:type cim:PositionPoint.
  
  ?postionPointId1 cim:PositionPoint.Location ?locationId.
  ?postionPointId2 cim:PositionPoint.Location ?locationId.
  
  
  #coordinates
  ?postionPointId1 cim:PositionPoint.xPosition ?xPosition1.
  ?postionPointId1 cim:PositionPoint.yPosition ?yPosition1.
  
  ?postionPointId2 cim:PositionPoint.xPosition ?xPosition2.
  ?postionPointId2 cim:PositionPoint.yPosition ?yPosition2.

  #construct WKTs (draw geometry)
  BIND(concat("POINT (", str(?xPosition1), " ", str(?yPosition1), ")") as ?pointStr)
  BIND(strdt(?pointStr, geo:wktLiteral) as ?g_wktPoint)
  
  BIND(concat("LINESTRING (", str(?xPosition1), " ", str(?yPosition1), ", ", str(?xPosition2), " ", str(?yPosition2), ")") as ?lineStr)
  BIND(strdt(?lineStr, geo:wktLiteral) as ?g_wktLine)

  FILTER(?xPosition1 != ?xPosition2 || ?yPosition1 != ?yPosition2)
}